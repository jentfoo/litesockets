apply from: 'build.shared'
apply plugin: 'jacoco'
apply plugin: 'eclipse'

test {
  maxParallelForks = Math.max(1, (int)(Runtime.getRuntime().availableProcessors() / 4))
  jacoco {
    excludes = ['**/package-info**','**/*Test']
    destinationFile = file("$buildDir/reports/jacoco/test.exec")
  }
}

build.dependsOn jacocoTestReport

jacocoTestReport {
  doFirst {
    classDirectories = fileTree(dir: 'build/classes/java/main', include: 'org/threadly/**')
    sourceDirectories = fileTree(dir: 'src/main/java', include: 'org/threadly/**')
  }
  reports {
    csv.enabled = false
    xml.enabled = true
    xml.destination = file("$buildDir/reports/jacoco/jacoco.xml")
    html.enabled = true
    html.destination = file("$buildDir/reports/jacoco/html")
  }
  doLast {
    println "Test results available at:"
    println "html - $buildDir/reports/tests/html/index.html"
    println "Test coverage reports available at:"
    println "html - $buildDir/reports/jacoco/html/index.html"
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'METHOD'
        minimum = 0.5
      }
    }
    rule {
      limit {
        counter = 'CLASS'
        minimum = 0.6
      }
    }
    rule {
      limit {
        counter = 'LINE'
        minimum = 0.5
      }
    }
    rule {
      limit {
        counter = 'BRANCH'
        minimum = 0.5
      }
    }
  }
}

check.dependsOn jacocoTestCoverageVerification
